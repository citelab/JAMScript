
/**
 * Copyright (c) 2015 Muthucumaru Maheswaran <mahes25@gmail.com>
 *
 * See the LICENSE file at the root of the project for the full license.
 *
 */


// Dependencies
var CParser     = require('../../c/grammars/c_parser.ojs'),
    ES5Parser   = require('../../../deps/es5/grammars/es5_parser.ojs'),
    __          = require('../nodes.js');
var namespace_funcs = [];
var sync_funcs      = [];

/**
 * C side of the JAMScript language..
 * Full definition of the grammar. This one extends the C parser
 */


ometa JAMCParser <: CParser  {

    namespace_spec  = "in" "Id":s                                       -> __.NamespaceSpec().name(s.value()),

    requires_spec   = "requires" "Id"+:t                                -> __.RequiresSpec(t),

    type_spec       = "jcallback":s                                     -> __.TypeSpec().type(s.value())
                    | ^type_spec,

    jamd_async_decl = "jasync" decl_specs:s declarator:d
                                    namespace_spec?:ns                  -> {this.addToNameSpace(d, ns); __.JDeclaration(s, d, ns).sync(false)},

    jamd_sync_decl  = "jsync" decl_specs:s declarator:d
                                    namespace_spec?:ns                  -> {this.addSyncFunc(d,ns);this.addToNameSpace(d, ns); __.JDeclaration(s, d, ns).sync(true)},

    oncomp_decl     = type_spec:t "oncomplete" "(" param_type_lst:d ")" -> __.ODeclaration(t, d).otype("complete"),

    onerror_decl    = type_spec:t "onerror" "(" param_type_lst:d ")"    -> __.ODeclaration(t, d).otype("error"),

    oncancel_decl   = type_spec:t "oncancel" "(" param_type_lst:d ")"   -> __.ODeclaration(t, d).otype("cancel"),

    onload_decl     = type_spec:t "onload" "(" param_type_lst:d ")"     -> __.ODeclaration(t, d).otype("load"),

    onwatch_decl    = type_spec:t "onwatch" "(" param_type_lst:d ")"    -> __.ODeclaration(t, d).otype("watch"),

    complete_block  = oncomp_decl:d ES5Parser.block:b ";"               -> __.CompleteBlock(d, b),

    error_block     = onerror_decl:d ES5Parser.block:b ";"              -> __.ErrorBlock(d, b),

    load_block      = onload_decl:d ES5Parser.block:b ";"               -> __.LoadBlock(d, b),

    watch_block     = onwatch_decl:d ("until" cond_expr:e)? ES5Parser.block:b ";" -> __.WatchBlock(d, b, e),

    complete_stmt   = oncomp_decl:d compound_stmt:s ";"                 -> __.CompleteStmt(d, s),

    error_stmt      = onerror_decl:d compound_stmt:s ";"                -> __.ErrorStmt(d, s),

    cancel_stmt     = oncancel_decl:d compound_stmt:s ";"               -> __.CancelStmt(d, s),

    load_stmt       = onload_decl:d compound_stmt:s ";"                 -> __.LoadStmt(d, s),

    watch_stmt      = onwatch_decl:d ("until" cond_expr:e)? compound_stmt:s ";" -> __.WatchStmt(d, s, e),

    c_as_activity   = jamd_async_decl:d "{" c_as_handlers*:h "}"        -> __.ActivityDef(d, h).type("c"),

    js_as_activity  = jamd_async_decl:d "{" js_as_handlers*:h "}"       -> __.ActivityDef(d, h).type("js"),

    c_as_handlers   = load_stmt
                    | watch_stmt
                    | complete_block
                    | error_block
                    | cancel_stmt,

    js_as_handlers  = load_block
    				| watch_block
                    | complete_stmt
                    | error_stmt,

    c_s_activity    = jamd_sync_decl:d compound_stmt:s               -> __.SyncActivityDef(d, s).type("c"),

    js_s_activity   = jamd_sync_decl:d ES5Parser.block:b             -> __.SyncActivityDef(d, b).type("js"),

    async_activity  = c_as_activity
                    | js_as_activity,

    sync_activity   = c_s_activity
                    | js_s_activity,

    activity_def    = sync_activity
                    | async_activity,

    function_def    = activity_def
                    | ^function_def,

    external_decl   = function_def
                    | declaration,

    program         = external_decl+
}

JAMCParser.addSyncFunc = function(decl, namespace) {
    var funcName = decl[1].name;
    if(namespace != undefined) {
        funcName = namespace[1].name + "." + funcName;
    }
    sync_funcs.push(funcName);
}
JAMCParser.addToNameSpace = function(decl, namespace) {
    if(namespace != undefined) {
        var namespace_name = namespace[1].name;
        if(namespace_funcs.indexOf(namespace_name) == -1) {
            namespace_funcs[namespace_name] = [];
        }
        namespace_funcs[namespace_name].push(decl[1].name);
    }
}

JAMCParser.parse = function(input) {

    // Add JAMScript C side keywords..
    this.spec.addKeywords(["jasync", "jsync", "in", "oncomplete", "onerror", "oncancel", "onload", 
    	"onwatch", "live", "jcallback", "requires", "until"]);

    return {
        tree: JAMCParser.matchAll(input, 'program'), 
        data: {
            'namespace_funcs': namespace_funcs,
            'sync_funcs': sync_funcs
        }
    };
};

module.exports = JAMCParser
