JAMJS <: ES6 {
    Declaration     += Jexport 
                    | Jrequire
                    | Jconditional
                    | Jdata_decl
                    | Activity_def

    Jexport         = jexport identifier at ("device" | "fog" | "cloud") ";"        -- specified
                    | jexport identifier ";"                                       -- default

    Jrequire        = jrequire identifier as identifier at ("fog" | "cloud" | "device") ";"

    jdata_type      = broadcaster
                    | logger

    C_type          = "struct" identifier "{" Struct_entry* "}"     -- struct
                    | "char" "*"                                    -- pointer
                    | "char"
                    | "double"
                    | "int"
                    | "float"

    Struct_entry    = C_type identifier ";"

    Jdata_spec      = C_type identifier as jdata_type "(" ("fog"|"cloud") ")" ";"   -- specified
                    | C_type identifier as jdata_type ";"                           -- default
                    | Flow ";"                                                      -- flow

    Flow            = identifier as flow with identifier of identifier              -- flow
                    | identifier as outflow of identifier                           -- outflow
                    | identifier as inflow                                          -- inflow


    Jdata_decl (a jdata declaration) = jdata "{" Jdata_spec* "}"

    Jconditional (a jconditional declaration) = jcond identifier? "{" Jcond_entry* "}"

    Jcond_entry     = identifier ":" NonemptyListOf<Jcond_rule, jcond_joiner> ";"

	Jcond_rule      = MemberExpr jcond_op MemberExpr ","? identifier?

    MemberExpr      = identifier "(" MemberExpr ")"                 -- funcExpr
                    | MemberExpr "." identifier                     -- propRefExp
                    | identifier
                    | literal

    jcond_op        = "==" | ">=" | ">" | "<=" | "<" | "!="

    jcond_joiner    = "&&" | "||"

    Async_activity  = jasync Jcond_specifier? FunctionDeclaration

    Sync_activity   = jsync Jcond_specifier? FunctionDeclaration

    Jcond_specifier = "{" Jcond_expr "}"

    Jcond_expr      = "(" Jcond_expr ")"                            -- paran
                    | "!" Jcond_expr                                -- not
                    | Jcond_expr jcond_expr_op Jcond_expr           -- bin_op
                    | identifier "." identifier                     -- namespace
                    | identifier                                    -- id

    jcond_expr_op   = "&&" | "||"
    
    Jtask_attr_spec = "[" ListOf<Jtask_attr, ",">  "]"
    
    Jtask_attr      = reuse "=" booleanLiteral
                    | reuse_limit "=" numericLiteral
                    | reuse_argdev "=" percentage
                    | max_rate "=" numericLiteral
                    
   	percentage 	    = numericLiteral "%"
    
    Jtask_activity  = function jtask Jcond_specifier? Jtask_attr_spec? identifier "(" FormalParameterList ")" "{" FunctionBody "}"

    Activity_def (a activity definition)  = Sync_activity
                                          | Async_activity
                                          | Jtask_activity

    as = "as" ~identifierPart
    at = "at" ~identifierPart
    beat = "beat" ~identifierPart
    broadcaster = "broadcaster" ~identifierPart
    controller = "controller" ~identifierPart
    flow = "flow" ~identifierPart
    is = "is" ~identifierPart
    inflow = "inflow" ~identifierPart
    jamtask = "jamtask" ~identifierPart
    jasync = "jasync" ~identifierPart
    jcond = "jcond" ~identifierPart
    jdata = "jdata" ~identifierPart
    jsync = "jsync" ~identifierPart
    jexport = "jexport" ~identifierPart
    jrequire = "jrequire" ~identifierPart
    linegraph = "linegraph" ~identifierPart
    logger = "logger" ~identifierPart
    name = "name" ~identifierPart
    of = "of" ~identifierPart
    outflow = "outflow" ~identifierPart
    source = "source" ~identifierPart
    terminal = "terminal" ~identifierPart
    type = "type" ~identifierPart
    jtask = "jtask" ~identifierPart
    reuse = "reuse" ~identifierPart
    reuse_limit = "reuse_limit" ~identifierPart
    reuse_argdev = "reuse_argdev" ~identifierPart
    max_rate = "max_rate" ~identifierPart
}